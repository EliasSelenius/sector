
include "../plang/common/builtin/types.pog";

include "../grax/grax.pog";

include "src/world.pog";
include "src/particles.pog";
include "src/inventory.pog";
include "src/agent.pog";

// learn to tolerate boredom

// ..\plang\bin\plang.exe src\main.pog cflags -lglfw3dll -O3
// ..\plang\bin\plang.exe src\main.pog cflags -lglfw3dll -O0 -g -fsanitize=address -fno-omit-frame-pointer
// ..\plang\bin\plang.exe src\main.pog cflags -lglfw3dll -O0 -g -fsanitize=address -fno-omit-frame-pointer -lkernel32 -lgdi32 -luser32 -lruntimeobject -lwinmm -ld3d11 -ldxguid -ld3dcompiler -lshlwapi -lole32 -lshcore -lavrt -lksuser -ldbghelp
// ..\plang\bin\plang.exe src\main.pog cflags -lglfw3dll -O0 -g -lkernel32 -lgdi32 -luser32 -lruntimeobject -lwinmm -ld3d11 -ldxguid -ld3dcompiler -lshlwapi -lole32 -lshcore -lavrt -lksuser -ldbghelp

/*
    Civilization is flourishing in an asteroid field of abundant riches, and yet conflict is prevalent
    goal of game is to establish dominance, competing with several other factions who also want sole control of the mineral rich asteroidbelt
*/

/* TODOs:
    - dust particles floating in space
    - asteroid cluster distribution

    - electric particle effect on damaged ships
    - overlay fire cracks on sprites for damaged ships


    - higher value ore drop in higher level sectors

    - ships drop data

    - research-tree: use data to unlock ship components
    - data used to unlock upgrades to ship components
        thrusters
        health (armor)
        shield
        shield_regen
        weapon
        tracktor beam
        projectile deflection chance
        ramming upgrade: smaller asteroids break when you hit

    tracktor beam upgrades:
        - radius
        - count

    - ship-outfitter: select a ship and components, build with money

    - explosive charges

    - biome: icy
    - biome: crystal
    - biome: dusty
    - biome: acidic

    - bounty system (makes hordes of enemies spawn to attack you)

Priority:

    - improve AI
        - detection range
        - passive behaviour
            - pickup items
            - destroy asteroid
            - drop cargo at spacestation
            - follow behaviour fleet leader

        - aggressive when provoked
            - loose aggression

    - seperate rendering and game logic into different codepaths
        - render map


idea:
    only able to mine asteroids with a mining laser
    laser can be upgraded to be able to:
     - mine larger asteroids
     - more ore yield from asteroids
     - break asteroid faster
     - range

*/

vec2 get_mouse_world_coord(Transform2D cam_tr) {
    float32 x = mouse_x as float32 / app.window_width * 2 - 1;
    float32 y = -(mouse_y as float32 / app.window_height * 2 - 1);

    x *= app.width_over_height;

    return local2world(cam_tr, x, y);
}


const debris_count = 6;
DrawBuffers[debris_count] db_debris;


Spritesheet spritesheet;
Spritesheet spritesheet_items;

const sprites_asteroids_count = 4;
Sprite[sprites_asteroids_count] sprites_asteroids;

Sprite[2] sprites_items;

Sprite sprite_turret;
Sprite sprite_spacestation;

Texture2D projectile;
Texture2D particle;

Sprite sprite_projectile;
Sprite sprite_particle;
Sprite sprite_shockwave;
Sprite sprite_cloud;
Sprite sprite_ftl_test;

Wav_File wav_hallo;
Wav_File wav_debris_break;

// struct Assets {
//     Spritesheet spritesheet;
//     Spritesheet spritesheet_items;

//     Sprite[] sprites;

//     Sprite*[sprites_asteroids_count] asteroids;
//     Sprite*[] spaceships;
//     Sprite*[ItemKind_Count] items;
// }

void loadassets() {

    wav_hallo = load_wav("assets/hallo.wav");
    wav_debris_break = load_wav("assets/debris-break-253779.wav");


    projectile = load_texture2D("assets/proj.bmp");
    particle   = load_texture2D("assets/particle.bmp");

    // set_filter(particle, TextureFilter.linear);
    // set_wrap_mode(particle, TextureWrapMode.clamp_to_edge);

    Sprite load_sprite(char* file) return make_sprite(load_texture2D(file));
    sprite_projectile = make_sprite(projectile);
    sprite_particle = make_sprite(particle);
    sprite_shockwave = load_sprite("assets/shockwave.bmp");
    sprite_cloud = load_sprite("assets/cloud.bmp");
    sprite_ftl_test = load_sprite("assets/ftl_test.bmp");

    // set_filter(sprite_cloud.tex, TextureFilter.linear);

    Spritesheet load_spritesheet(char* file_name) {
        let image = load_bitmap(file_name);
        Spritesheet sheet = {};
        sheet.regions = get_all_regions(image);

        // for 0 .. image.width * image.height {
        //     if equals(image.pixels[it], Black) {
        //         image.pixels[it] = {};
        //     }
        // }

        sheet.texture = create_texture2D(image);
        set_filter(sheet.texture, TextureFilter.nearest);


        free(image.pixels);
        return sheet;
    }

    spritesheet = load_spritesheet("assets/spritesheet.bmp");
    spritesheet_items = load_spritesheet("assets/Items.bmp");

    for 0 .. ship_types.length {
        ship_types[it].sprite = get_sprite(*spritesheet, ship_types[it].sprite_index);
    }

    ship_types[9].sprite = sprite_ftl_test;


    for 0 .. sprites_asteroids_count {
        sprites_asteroids[it] = get_sprite(*spritesheet, it);
    }

    sprites_asteroids[3].is_semi_transparent = true;

    for 0 .. ItemKind_Count {
        sprites_items[it] = get_sprite(*spritesheet_items, it);
    }

    sprite_turret = get_sprite(*spritesheet, 10);
    sprite_spacestation = get_sprite(*spritesheet, 16);

    sprite_projectile.is_semi_transparent = true;
    sprite_shockwave.is_semi_transparent = true;
    sprite_particle.is_semi_transparent = true;
    sprite_cloud.is_semi_transparent = true;
}

enum Menu_Tabs { Ships; Cargo; Tech; Map; Esc; Count; }
char*[] tab_names = {"ships", "cargo", "tech", "map", "esc"};
bool menu_open = false;
Menu_Tabs menu_tab = .Ships;

Transform2D camera_world = {{0,0}, 0, 10};
Transform2D camera_map = {scale = 6000};
Transform2D camera_to_dispatch;

RenderLayer world_ui_layer;
RenderLayer static_ui_layer;

void on_event(AppEvent event, AppEventData data) switch event {

}

void main() {

    // { // test
    //     void test(uint32 i) {
    //         let f = @(*i as float32*);
    //         print("float: ", f, " = ", to_string(f), "\n");
    //     }

    //     test(0x7f80_0000);
    //     test(0x7f80_0001);
    //     test(0x7f80_0002);
    //     test(0x7f80_0003);
    //     test(0x7f80_0004);
    //     test(0x7f80_0005);
    //     test(0x7f80_0006);
    //     test(0x7f80_0007);
    //     test(0x7f80_0008);
    //     test(0x7f80_0009);
    //     test(0x7f80_000a);
    //     test(0x7f80_000b);
    //     test(0x7f80_000c);
    //     test(0x7f80_000d);
    //     test(0x7f80_000e);
    //     test(0x7f80_000f);

    //     test(0xffc0_0000);
    //     test(0x7fc0_0000);

    //     test(0xff_800001);
    //     test(0xff_800000);

    //     test(0x0000_0000);

    //     float32 div_by_zero = 12.0/0.0;
    //     test(@(*div_by_zero as uint32*));

    //     float32 sqrt_neg = sqrtf(-1);
    //     test(@(*sqrt_neg as uint32*));

    // } return;

    grax_init();
    app.on_event = on_event;
    init_draw2d();

    loadassets();

    for 0 .. debris_count {
        db_debris[it] = create_draw_buffers(sizeof vertex2D, vertex2D_attribs);

        vec2 clamp_to_square_edge(float32 x, y) {
            if abs(x) < abs(y) {
                y /= abs(y);
            } else {
                x /= abs(x);
            }

            return make_vec(x, y) * 0.5;
        }

        vertex2D[3] vs;
        vs[0] = {0, 0, 0.5, 0.5, White};

        let t = it as float32 / debris_count * Tau;
        vec2 p = clamp_to_square_edge(cos(t), sin(t));
        vs[1] = {p, p + vec2_one*0.5, White};

        t = (it+1) as float32 / debris_count * Tau;
        p = clamp_to_square_edge(cos(t), sin(t));
        vs[2] = {p, p + vec2_one*0.5, White};

        uint32[] inds = { 0, 1, 2 };
        vertex2D[] verts; // TODO: vertex2D[3] should implicitly convert to vertex2D[]
        verts.data = *vs;
        verts.length = 3;
        update_buffers(*db_debris[it], verts, inds);
    }


    set_clear_color(rgba(0x0a0a0aff));

    init_chunks();

    world_ui_layer = create_layer();
    static_ui_layer = create_layer();
    immediate_layer = *static_ui_layer;

    {
        print("sizeof Entity = ", sizeof Entity, " bytes.\n");
    }

    gen_world(0);

    audio_init(*wav_hallo);
    while grax_loop() {
        audio_loop(make_vec(camera_world.pos, camera_world.scale), 0.2);

        static bool draw_list = false;
        if key_pressed(GLFW_KEY_F2) draw_list = !draw_list;
        if draw_list draw_entity_list();

        if key_pressed(Key.tab) {
            menu_open = !menu_open;
            camera_map = camera_world;
            camera_map.scale *= 2;
        }

        if menu_open {
            draw_menu();
        } else {
            update_world();

            draw_gui();
            vec2 center = {1.2, 0.7};
            draw_minimap(center);
            camera_to_dispatch = camera_world;
        }

        if key_pressed(Key.backspace) {
            unlock_all_ships();
        }

        if key_pressed('Y') {
            save_game();
        } else if key_pressed('U') {
            load_game();
        }

        if key_pressed('R') {
            default2d_shader = load_shader(default2d_shader.name.chars);
        }

        glDepthMask(true);
        glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT);
        dispatch_sprites(camera_to_dispatch);
        dispatch_layer(*world_ui_layer, camera_to_dispatch);
        dispatch_layer(*static_ui_layer, {scale=1} as Transform2D);

        if key_pressed(GLFW_KEY_ESCAPE) break;
        this_frame = {};
    }
}

ThisFrameData this_frame;
struct ThisFrameData {
    bool player_pickup_item;
    bool player_took_damage;
    bool target_took_damage;
}


SpaceshipType* ship_selection_menu() {
    SpaceshipType* result = null;

    let l = *world_ui_layer;

    static Transform2D menu_cam_tr = {scale=200};
    mouse_drag_control(*menu_cam_tr);
    camera_to_dispatch = menu_cam_tr;

    vec2 mouse_world_pos = get_mouse_world_coord(menu_cam_tr);

    float32 horz_pos = -1;
    for 0 .. ship_types.length {
        let st = *ship_types[it];

        float32 stride = st.sprite.uv_scale.x * st.sprite.tex.width;
        float32 height = st.sprite.uv_scale.y * st.sprite.tex.height;
        float32 start = horz_pos;

        Transform2D tr = {};
        tr.scale = 1;
        tr.pos = {horz_pos + stride/2, height/2};
        horz_pos += stride + 30;

        float32 glyph_height = 10;
        vec4 color = vec4_one;
        vec4 outline = vec4_zero;


        if is_unlocked(st) {
            vec2 size = { stride, height };
            vec2 bb_min = tr.pos - size/2;
            vec2 bb_max = tr.pos + size/2;

            if point_inside_bb(mouse_world_pos, bb_min, bb_max) {
                outline = {1,1,1,1};
                tr.scale = 1.1;
                if mouse_down(Left_Mouse) {
                    result = st;
                }
            }

            draw_text(l, tr.pos + make_vec(stride/2, height/2), glyph_height, to_string(st.hp as uint32), Red);
            draw_text(l, tr.pos + make_vec(stride/2, height/2 - glyph_height), glyph_height, to_string(st.cargo_capacity), Blue);

            draw_text(l, tr.pos + make_vec(-stride/2, height/2), glyph_height, to_string(st.build_cost), Cyan);
        } else {
            uint32 prct = unlock_percent(st);
            let text = make_string(concat(to_string(prct), "%"));
            draw_text(l, tr.pos - make_vec(5*(text.length-1 as float32)/2.0, 0), glyph_height, text, White);

            color = {0,0,0, 1};
        }

        draw_sprite(tr, 0, st.sprite, color, vec4_zero, outline);

        draw_text(l, tr.pos + make_vec(-stride/2, -height/2), glyph_height, st.name, White);
    }

    return result;
}

void draw_menu() {

    let old_tab = menu_tab;
    draw_tabs(*static_ui_layer);
    if old_tab != menu_tab and menu_tab == .Map {
        camera_map = camera_world;
        camera_map.scale *= 2;
    }

    switch (menu_tab) {
    case .Ships: {
        float32 h = 0.07*1.5;
        draw_credits(app.top_left + {h, -h}, h);

        let st = ship_selection_menu();
        if st {
            if player_credits >= st.build_cost {
                player_credits -= st.build_cost;
                menu_open = false;

                player = spawn_ship(camera_world.pos + random_vec2(chunk_size*0.5), st, team_id_player);
                list_add(*player_fleet, *player);
            }
        }
    } break;
    case .Cargo: break;
    case .Tech: break;
    case .Map: {

        mouse_drag_control(*camera_map);
        camera_to_dispatch = camera_map;

        let l = *world_ui_layer;

        // draw_minimap(vec2_zero);


        // TODO: hmm... should we draw the background on the map?
        // draw_background(camera_map.pos);

        for y: 0 .. sector_count_sqrt for x: 0 .. sector_count_sqrt {

            let sector = index_sectors(x, y);
            // let north = index_sectors(x, y+1);
            // if north and north.faction_team_id != sector.faction_team_id {

            // }


            float32 opa = 0.1;
            Color bg_color = opacity(sector.discovered ? White : Gray, opa);
            Color tx_color = opacity(Black, opa);

            ivec2 spos = {x, y} - make_ivec2(sector_count_sqrt/2);
            vec2 pos = make_vec(spos) * sector_size;
            vec2 size = {sector_size, sector_size};
            UI_Rect rect = {pos, size};
            float32 pixel_size = 2.0 / app.window_height * camera_map.scale;
            rect.size = rect.size - vec2_one*pixel_size;
            draw_rect(l, rect, bg_color);

            let level = get_level(pos);
            let str = to_string(level);
            float32 glyph_height = rect.size.y * 0.4;
            draw_text(l, center_text(rect, glyph_height, str), glyph_height, str, tx_color);

            draw_rect(l, sector.biome_center, vec2_one*pixel_size*2, Green);
        }

        for 0 .. entity_pool.count {
            let en = *entity_pool.data[it];
            if is_dead(en) continue;

            vec4 color_fac = vec4_one;
            vec4 color_add = vec4_zero;
            vec4 color_out = vec4_zero;
            if en.entity_type == .ship {
                float32 t = (sin(app.total_run_time*5.0) + 1.0) / 2.0;
                float32 w = lerp(t, 0.4, 1);
                if en.team_id == team_id_player {
                    color_fac = {0.2,0.2,w,1};
                    color_add = {0,0,w,1};
                } else {
                    color_fac = {w,0.2,0.2,1};
                    color_add = {w,0,0,1};
                }
            } else if en.entity_type == .asteroid {
                float32 v = 0.3;
                color_fac = {v,v,v,1};
            }

            DrawBuffers db = en.entity_type == .debris ? db_debris[en.level] : draw2d_renderer.quad_db;
            draw_sprite(db, en.tr, en.depth, en.sprite, color_fac, color_add, color_out);
        }
    } break;
    case .Esc: break;
    }
}

void draw_tabs(RenderLayer* l) {

    if key_pressed('A') menu_tab = (menu_tab-1) % Menu_Tabs.Count;
    if key_pressed('D') menu_tab = (menu_tab+1) % Menu_Tabs.Count;

    float32 tab_width = 2.0 / tab_names.length;
    float32 tab_height = 0.1;
    vec2 size = {tab_width, tab_height};

    vec2 pos = {-1, 1 - tab_height};
    for 0 .. tab_names.length {


        let p = pos + make_vec((it as float32 + 0.5) * tab_width, 0);
        let s = size * make_vec(0.9, 1);
        UI_Rect rect = {p,s};

        Color color = White;
        if hover(rect, Input.mouse_pos) {
            color = Gray;
            if mouse_pressed(Left_Mouse) menu_tab = it;
        }

        if it == menu_tab {
            color = Blue;
        }

        let name = make_string(tab_names[it]);

        draw_rect(l, p, s, color);
        draw_text(l, center_text(rect, s.y, name), s.y, name, Black);
        // pos.x += 0.2;
    }
}

void draw_minimap(vec2 center) {
    float32 px = 2.0 / app.window_height;

    float32 scale = 0.12;
    let center_sector = get_sector_coord(camera_world.pos);
    for y: -1 .. 2   for x: -1 .. 2 {
        let pos = camera_world.pos / sector_size;
        let sector_pos = center_sector + {x, y};
        uint32 sector_level = (abs(sector_pos.x) + abs(sector_pos.y)) as uint32;
        vec2 screen_pos = center + (sector_pos - pos) * scale;

        UI_Rect rect = {screen_pos, {scale, scale}};

        draw_rect(pad(rect, 1*px), opacity(Gray, 0.3));
        draw_text(screen_pos, 0.40 * scale, to_string(sector_level), Black);
    }

    vec2 world2minimap(vec2 wpos) {
        float32 scale = 0.12;
        return (wpos - camera_world.pos) / sector_size * scale;
    }

    for 0 .. entity_pool.count {
        let en = *entity_pool.data[it];
        if is_dead(en) continue;

        Color color = Blue;
        float32 m = 2;
        switch (en.entity_type) {
            case .ship:     color = Red;    m = 2; break;
            case .station:  color = Blue;   m = 3; break;
            default: continue;
        }

        // make_vec2(0.07*scale)

        UI_Rect rect = {center + world2minimap(en.tr.pos)};
        rect.size = {px, px} * m;

        draw_rect(rect, color);
    }

    draw_rect(center, make_vec2(px*4), Green);
}

struct Blueprint_Unlcok_Progress_Popup {
    SpaceshipType* st;
    float32 time;
}

Blueprint_Unlcok_Progress_Popup popup_blueprint;

void popup_blueprint_progress(SpaceshipType* st) {
    popup_blueprint.st = st;
    popup_blueprint.time = 0;
}


void draw_bar(UI_Rect rect, float32* time, float32 value, max_value, Color target_color) {
    float32 px = 2.0 / app.window_height;

    Color bar_bg  = rgba(0xA0_A0_A0_A0);
    Color color_flash = lerp(@time, target_color, White);

    float32 progress = value / max_value;
    UI_Rect rect_progress = scale_pivot(pad(rect, 4*px), progress, lerp(@time, 1, 1.5),  -1, 0);

    draw_rect(rect, bar_bg);
    draw_rect(rect_progress, color_flash);

    float32 h = rect.size.y;
    UI_Rect rect_txt = child(rect_progress, 1,0,   h/2.0, h);

    uint32 display_value = value as uint32;
    uint32 display_max_value = max_value as uint32;

    if display_value != 0 and display_value != display_max_value {
        let str_value = to_string(display_value);
        vec2 pos = rect_txt.center;
        draw_text_backwards(pos, h, str_value, White);
    }

    let str_max_value = to_string(display_max_value);
    vec2 max_value_pos = point(rect, 1, 0) + {h/2.0, 0};
    draw_text(max_value_pos, h, str_max_value, target_color);

    let dt = deltatime();
    float32 rate = 8;
    @time = exp_decay(@time, 0, rate, dt);
}

void draw_credits(vec2 pos, float32 height) {
    float32 dt = deltatime();

    static float32 credits_display_interp = 1.0;

    if delay_payment <= 0.0 {
        if credits_to_add {
            player_credits++;
            credits_to_add--;
            credits_display_interp = 0;
        }
    } else {
        delay_payment -= dt;
    }

    Color regular_color = Cyan;

    Color c = lerp(credits_display_interp, White, regular_color);
    float32 h = lerp(credits_display_interp, height*1.4, height);
    credits_display_interp = exp_decay(credits_display_interp, 1, 6, dt);

    pos = draw_text(pos, h, to_string(player_credits), c);
    if credits_to_add {
        pos = draw_text(pos, height, concat(" +", to_string(credits_to_add)), regular_color);
    }

    float32 half = height*0.5;
    draw_text(pos + {0, -half/2.0}, half, "credits", regular_color);
}

void draw_gui() {
    float32 px = 2.0 / app.window_height;
    UI_Rect rect_window = rect_pp(app.bottom_left, app.top_right);

    float32 h = 0.07*1.5;
    draw_credits(app.top_left + {h, -h}, h);

    {
        uint32 fleet_count = list_length(player_fleet);
        UI_Rect rect_fleet = child(rect_window, -1,0,   100*px, fleet_count*50*px);
        draw_rect(rect_fleet, opacity(Black, 0.6));
        for 0 .. fleet_count {
            let en = player_fleet[it];
            UI_Rect rect_ship = div_y(rect_fleet, fleet_count, it);
            draw_rect(pad(rect_ship, px), opacity(White, 0.4));
        }
    }

    if player {

        static float32 capbar_t = 0;
        static float32 hpbar_t  = 0;
        static float32 tarbar_t = 0;

        if this_frame.player_pickup_item  capbar_t = 1;
        if this_frame.player_took_damage  hpbar_t  = 1;
        if this_frame.target_took_damage  tarbar_t = 1;

        Color color_cargo  = rgba(0x10_60_E0_F0);
        Color color_health = rgba(0xFF_60_40_F0);

        float32 width = 1;

        uint32 cargo_amt = cargo_amount(player);
        uint32 cargo_max = player.cargo_capacity;

        float32 height = 0.055;
        UI_Rect rect_capbar = translate(child(rect_window, -1,-1,  width, height), height, height);
        UI_Rect rect_hpbar  = translate(rect_capbar, 0, height * 1.1);

        if cargo_amt == cargo_max {
            float32 a = lerp((sinf(Tau * app.total_run_time as float32) + 1.0)*0.5, 0.1, 1);
            Color flash_color = opacity(color_cargo, a);

            vec2 pos = rect_capbar.center + {rect_capbar.size.x/2.0 + 2.0 * height, 0};
            draw_text(pos, height, "Drop Cargo: 'Q'", flash_color);
        }

        draw_bar(rect_capbar, *capbar_t, cargo_amt as float32, cargo_max as float32, color_cargo);
        draw_bar(rect_hpbar,  *hpbar_t, player.hp, player.max_hp, color_health);

        if player.target {
            UI_Rect rect_tarbar = rect_capbar;
            rect_tarbar.center.x = -rect_tarbar.center.x;
            draw_bar(rect_tarbar, *tarbar_t, player.target.hp, player.target.max_hp, color_health);

            float32 h = rect_tarbar.size.y;
            draw_text(point(rect_tarbar, -1, 1.5), h, entity_to_str(player.target), White);
        }

        { // speedometer
            float32 h = height*0.2;
            UI_Rect rect_spomet = {
                center = rect_hpbar.center + {0, (rect_hpbar.size.y + h)/2.0},
                size = {width, h}
            };

            float32 speed = length(player.vel);
            float32 max_speed = player.max_speed;
            UI_Rect rect_fill = scale_pivot(rect_spomet,  speed/max_speed, 1,   -1, 0);

            Color bg_color = opacity(Black, 0.3);
            Color color = opacity(Blue, 0.7);

            draw_rect(rect_spomet, bg_color);
            draw_rect(rect_fill, color);
        }
    }

    if popup_blueprint.st {
        let st = popup_blueprint.st;
        let t  = popup_blueprint.time;

        float32 t0 = clamp01(t*4);
        float32 y = lerp(t0, -0.1, 0);

        float32 opa = lerp(t0, 0, 0.75);

        vec2 pos = make_vec(0, -0.5 + y);
        draw_rect(pos, make_vec(1, 0.5), opacity(Black, opa));
        draw_text(pos, 0.1, concat(to_string(unlock_percent(st)), "%"), opacity(White, opa));

        popup_blueprint.time += deltatime();
        if popup_blueprint.time > 5 {
            popup_blueprint.st = null;
        }
    }
}

char* vec_to_str(vec2 v) return vec_to_str(temp_builder(), v);
char* vec_to_str(StringBuilder* sb, vec2 v) {
    to_string(sb, v.x);
    sb_append(sb, ", ");
    to_string(sb, v.y);
    return sb.content;
}

void draw_entity_properties_menu(Entity* en) {
    if en == null return;


    float32 text_height = 0.03;

    vec2 pos = {-1, 1 - text_height};

    // pos = (pos - camera_world.pos) / camera_world.scale;

    pos = draw_text(pos, text_height, concat("ID              : ", to_string(entity_index(en)), "\n"), White);
    pos = draw_text(pos, text_height, concat("parent          : ", entity_to_str(en.parent), "\n"), White);
    pos = draw_text(pos, text_height, concat("target          : ", entity_to_str(en.target), "\n"), White);
    pos = draw_text(pos, text_height, concat("entity_type     : ", entity_to_str(en), "\n"), White);
    pos = draw_text(pos, text_height, concat("behaviour       : ", "", "\n"), White);
    pos = draw_text(pos, text_height, concat("target_pos      : ", vec_to_str(en.target_pos), "\n"), White);
    pos = draw_text(pos, text_height, concat("tr              : ", "", "\n"), White);
    pos = draw_text(pos, text_height, concat("  .pos          : ", vec_to_str(en.tr.pos), "\n"), White);

    // pos = draw_text(pos, text_height, concat("  .rot          : ", to_string(en.tr.rot), "\n"), White);
    pos = draw_text(pos, text_height, "  .rot          : ", White);
    field_float(*en.tr.rot, pos, text_height);
    pos.y -= text_height;
    pos.x = -1;

    pos = draw_text(pos, text_height, concat("  .scale        : ", to_string(en.tr.scale), "\n"), White);
    pos = draw_text(pos, text_height, concat("depth           : ", to_string(en.depth), "\n"), White);
    pos = draw_text(pos, text_height, concat("sprite          : ", "", "\n"), White);
    pos = draw_text(pos, text_height, concat("color_factor    : ", "", "\n"), White);
    pos = draw_text(pos, text_height, concat("color_additive  : ", "", "\n"), White);
    pos = draw_text(pos, text_height, concat("vel             : ", vec_to_str(en.vel), "\n"), White);
    pos = draw_text(pos, text_height, concat("ang_vel         : ", to_string(en.ang_vel), "\n"), White);
    pos = draw_text(pos, text_height, concat("mass            : ", to_string(en.mass), "\n"), White);
    pos = draw_text(pos, text_height, concat("collider_radius : ", to_string(en.collider_radius), "\n"), White);
    pos = draw_text(pos, text_height, concat("level           : ", to_string(en.level), "\n"), White);
    pos = draw_text(pos, text_height, concat("hp              : ", to_string(en.hp), "\n"), White);
    pos = draw_text(pos, text_height, concat("max_hp          : ", to_string(en.max_hp), "\n"), White);
    pos = draw_text(pos, text_height, concat("team_id         : ", to_string(en.level), "\n"), White);
    pos = draw_text(pos, text_height, concat("cooldown        : ", to_string(en.cooldown), "\n"), White);
    pos = draw_text(pos, text_height, concat("cargo_capacity  : ", to_string(en.cargo_capacity), "\n"), White);
    pos = draw_text(pos, text_height, concat("cargo           : ", "", "\n"), White);
    pos = draw_text(pos, text_height, concat("item            : ", "", "\n"), White);

}

void draw_entity_list() {

    static float32 vert_pos = 0;
    static float32 vert_pos_target = 0;
    static Entity* selected_entity = null;

    float32 text_height = 0.03;


    int64 index = -1;
    if Input.mouse_pos.x < -1 {
        index = round((app.top_left.y + vert_pos - Input.mouse_pos.y - text_height) / text_height) as int64;
    }

    vert_pos = exp_decay(vert_pos, vert_pos_target, 10, deltatime());

    uint32 line_count = 0;
    vec2 pos = app.top_left + { text_height/2, -text_height + vert_pos };
    for 0 .. entity_pool.count {
        let en = *entity_pool.data[it];
        char* name = entity_type_names[en.entity_type];

        if en.entity_type == EntityType.asteroid {
            index++;
            continue;
        }

        Color color = White;
        if it == index {
            color = Blue;
            if mouse(Left_Mouse) {
                selected_entity = en;
                // player = en;
                // camera_world.pos = en.tr.pos;
            }
        }

        draw_text(pos, text_height, concat(to_string(it as uint64).chars, ". ", name), color);
        pos.y -= text_height;
        line_count++;
    }

    if Input.mouse_pos.x < -1 {
        vert_pos_target = clamp(vert_pos_target - Input.mouse_scroll/10, 0, line_count * text_height);
        Input.mouse_scroll = 0;
    }



    draw_entity_properties_menu(selected_entity);
    if selected_entity  selected_entity.color_additive = vec4_one;// * 2;

    { // test
        static float32 v = 12;
        field_float(*v, make_vec(0, 0), 0.05);
    }

}

void field_float(float32* value, vec2 pos, float32 height) {
    // anchor = .bottom_left;


    let text = to_string(@value);
    vec2 size = make_vec(height/2 * text.length, height);

    vec2 v = (Input.mouse_pos - pos);
    if v.x < size.x and v.y < size.y and v.x > 0 and v.y > 0 {
        draw_rect(pos + make_vec(size.x/2 - height/4, 0), size, opacity(White, 0.3));

        @value += Input.mouse_scroll / (key_down(GLFW_KEY_LEFT_SHIFT) ? 10 : 1);
        Input.mouse_scroll = 0;
    }

    draw_text(pos, height, text, White);
    // anchor = .center;
}

    // widget_push();
    // background(image)
    // text("dawodjaw")
    // text()
    // if hover() {
    //     border(Red);
    // } else {
    //     border(Blue);
    // }
    // widget_pop();

// void slider(vec2 pos, size, float32* value) {
// }



struct Game_Save {
    uint32 player_credits;
    vec2   player_pos;
    uint32 player_ship_type;

    Cargo player_cargo;

    uint32[Ship_Type_Count] blueprints;
}


void save_game() {
    Game_Save save = {};
    save.player_credits = player_credits;
    if player {
        save.player_pos = player.tr.pos;
        save.player_ship_type = 1 + (player.ship_type as uint64 - ship_types.data as uint64) / sizeof SpaceshipType;
        save.player_cargo = player.cargo ? @player.cargo : {};
    } else {
        save.player_pos = camera_world.pos;
    }

    for 0 .. ship_types.length {
        save.blueprints[it] = ship_types[it].blueprints_acquired;
    }

    write_binary_file("save.bin", *save, sizeof Game_Save);
}

void load_game() {
    let buffer = read_binary_file("save.bin");
    Game_Save* save = buffer.data;

    player_credits = save.player_credits;

    camera_world.pos = save.player_pos;
    if save.player_ship_type {
        player = spawn_ship(save.player_pos, save.player_ship_type - 1, team_id_player);
        @player.cargo = save.player_cargo;
    } else {
        player = null;
    }

    for 0 .. ship_types.length {
        (*ship_types[it]).blueprints_acquired = save.blueprints[it];
    }
}